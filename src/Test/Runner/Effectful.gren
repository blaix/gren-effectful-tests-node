module Test.Runner.Effectful exposing 
    ( Program

    -- TESTS
    , Test
    , test

    -- TASKS
    , await
    , awaitError

    -- RUN
    , run
    --, runWithOptions
    , init
    , thenRun
    --, thenRunWithOptions

    -- ORGANIZATION
    , concat
    , describe
    --, todo
    --, skip
    --, only
    
    -- FUZZ TESTING
    --, fuzz
    --, fuzz2
    --, fuzz3
    )


{-| Effectful Test Runner

A test runner that supports Tasks, letting your verify their results.

See the [README](https://github.com/blaix/gren-effectful-tests-node/blob/main/README.md)
or [example folder](https://github.com/blaix/gren-effectful-tests-node/tree/main/example)
for usage examples.

@docs Program

## Tests

@docs Test, test

## Tasks

@docs await, awaitError

## Running Tests

@docs run

## Initializing Subsystems

@docs init, thenRun

## Test Organization

@docs concat, describe
    
## Fuzz Testing

TODO

-}

import Expect exposing (Expectation)
import Init
import Node
import Random
import Task exposing (Task)
import Terminal
import Test
import Test.Runner.Log as Log
import Test.Runner.Node
import Time


{-| Type alias for a program that runs your effectful test suite.
-}
type alias Program a =
    Node.SimpleProgram a


-- TESTS


{-| A test in an effectful test suite.

They can be created with [test](#test), [fuzz](#fuzz), or several other functions in this module:
    
    import Expect
    import Test.Runner.Effectful exposing (test)

    test "my test" <| \_ ->
        Expect.equal True True

The semantics are the same as a test from the
[core test package](https://packages.gren-lang.org/package/gren-lang/test/version/latest/module/Test#test),
except they can wrapped in one or more [await](#await) or [awaitError](#awaitError) calls to verify the outcome a 
[Task](https://packages.gren-lang.org/package/gren-lang/core/version/latest/module/Task):

    import Expect
    import Test.Runner.Effectful exposing (await, test)
    import Time

    await Time.now "The current time" <| \now ->
        test "is not Jan 1, 1970" <| \_ ->
            Expect.notEqual (Time.millisToPosix 0) now
-}
type alias Test =
    Task Never (InternalTest {})


type InternalTest a
    = Box a
    | Ready Test.Test


unwrap : InternalTest {} -> Test.Test
unwrap internalTest =
    when internalTest is
        Ready t ->
            t
        Box _ ->
            -- This should never happen...
            -- Any way to refactor it away?
            Test.test "Unexpected error" <| \_ ->
                Expect.fail
                    """
                    Oops! Something weird happened and you reached a point in
                    the code that I thought was unreachable. Congrats! But
                    also... sorry about that. It would be great if you could
                    open an issue so I can figure out how this happened:
                    https://github.com/blaix/gren-effectful-tests-node/issues
                    Please include the test(s) that triggered this error.
                    """


{-| Create a [Test](#Test).
    
    test "1 + 1 = 2" <| \_ ->
        Expect.equal 2 (1 + 1)
-}
test : String -> ({} -> Expectation) -> Test
test description thunk =
    Test.test description thunk
        |> Ready
        |> Task.succeed



-- RESOLVING TASKS


{-| Wait for a task to resolve before running a test.

    await Time.now "Current time" <| \now ->
        test "is not Jan 1, 1970" <| \_ ->
            Expect.notEqual (Time.millisToPosix 0) now

If the task fails, the inner test won't run and the test suite will fail.
If you want to test the error condition of a task, use [awaitError](#awaitError).

You can nest as many awaits as you want:

    await (Task.succeed "a") "task a" <| \a ->
    await (Task.succeed "b") "task b" <| \b ->
        test "are not equal" <| \_ ->
            Expect.notEqual a b
-}
await : Task error a -> String -> (a -> Test) -> Test
await task description callback =
    task
        |> Task.map Box
        |> Task.onError 
            (\e -> 
                (\_ -> Expect.fail ("Task failed with: " ++ (Debug.toString e)))
                    |> Test.test description
                    |> Ready
                    |> Task.succeed
            )
        |> Task.andThen (map callback)
        -- TODO: am I double wrapping description?
        |> Task.andThen (wrapDescription description)


{-| Test the error state of a task.

    awaitError (Task.fail "oopsy") "An expected failure" <| \error ->
        test "is an oopsy" \_ ->
            Expect.equal "oopsy" error

If the task does not fail, the inner test won't run and the test suite will fail.

These can be nested just like (and alongside) [await](#await) calls.
-}
awaitError : Task error a -> String -> (error -> Test) -> Test
awaitError task description callback =
    task
        |> Task.map Box
        |> Task.andThen 
            (\a -> 
                (\_ -> Expect.fail ("Expected error, but got non-error value: " ++ (Debug.toString a)))
                    |> Test.test description
                    |> Ready
                    |> Task.succeed
            )
        |> Task.onError callback
        -- TODO: am I double wrapping description?
        |> Task.andThen (wrapDescription description)


map : (a -> Test) -> InternalTest a -> Test
map fn internalTest =
    when internalTest is
        Box a ->
            fn a
        Ready t ->
            Task.succeed (Ready t)


wrapDescription : String -> InternalTest {} -> Test
wrapDescription description internalTest =
    internalTest
        |> unwrap
        |> Array.singleton
        |> Test.describe description
        |> Ready
        |> Task.succeed


-- RUNNING TESTS


{-| Create a program that runs your effectful test suite.

This will normally be used as your `main` function:

    main =
        run <|
            await (Task.succeed "hi") "my task" <| \val ->
                test "says hi" <| \_ ->
                    Expect.equal "hi" val
-}
run : Test -> Program a
run t =
    Node.defineSimpleProgram <| \env ->
        thenRun env t


{-| Customize a test run with [Options](#Options).
-}
runWithOptions : Options -> Test -> Program a
runWithOptions options t =
    Node.defineSimpleProgram <| \env ->
        thenRunWithOptions env options t


{-| Options to customize your test run.

    * `runs`: how many times a [fuzz](#fuzz) test is run with different inputs.
    * `seed`: used to generate random input in fuzz tests. A test suite run with the same seed will generate the same fuzz values.
-}
type alias Options =
    { runs : Int
    , seed : Random.Seed
    }


-- INITIALIZING SUBSYSTEMS


{-| Gives your test suite access to
[Node.Environment](https://packages.gren-lang.org/package/gren-lang/node/version/latest/module/Node#Environment)
and [subsystem permissions](https://packages.gren-lang.org/package/gren-lang/node/version/latest/module/Init).

This call will wrap your test suite, which will need to be run with
[thenRun](#thenRun) instead of [run](#run). For example:

    main =
        init <| \env ->
            FileSystem.initialize <| \fsPerm ->
            HttpClient.initialize <| \httpPerm ->
                thenRun env <|
                    await (myReadTask fsPerm) "read file" <| \contents ->
                    await (myHttpTask httpPerm) "send contents" <| \response ->
                        test "is successfull" <| \_ ->
                            Expect.equal 200 response.statusCode
-}
init : (Node.Environment -> Init.Task (Cmd a)) -> Program a
init =
    Node.defineSimpleProgram


{-| Define the [Test](#Test) suite that should run after initializing subsystems.

The semantics of this functino are equivalent to [run](#run),
except it takes [Node.Environment](https://packages.gren-lang.org/package/gren-lang/node/version/latest/module/Node#Environment)
in addition to the test suite.
-}
thenRun : Node.Environment -> Test -> Init.Task (Cmd a)
thenRun env test_ =
    Init.awaitTask Time.now <| \time ->
        let
            options =
                { runs = 100
                , seed = Random.initialSeed (Time.posixToMillis time)
                }
        in
        thenRunWithOptions env options test_


{-| Like [thenRun](#thenRun) but allows you to customize with [Options](#Options).
-}
thenRunWithOptions : Node.Environment -> Options -> Test -> Init.Task (Cmd a)
thenRunWithOptions env options test_ =
    Init.await Terminal.initialize <| \termConfig ->
    Init.awaitTask test_ <| \t ->
        Node.endSimpleProgram <|
            Log.run
                { test = unwrap t
                , runs = options.runs
                , passStream = env.stdout
                , failStream = env.stderr
                , colorDepth = getColorDepth termConfig 
                , seed = options.seed
                }


-- TEST ORGANIZATION


{-| Group an array of [Test](#Test)s into one.
    
    concat
        [ test "true is true" <| \_ ->
            Expect.equal True True
        , test "false is false" <| \_ ->
            Expect.equal False False
        ]
-}
concat : Array Test -> Test
concat tests =
    tests
        |> Array.map (Task.map unwrap)
        |> Task.sequence
        |> Task.map Test.concat
        |> Task.map Ready


{-| Group an array of [Test](#Test)s with a description.

    describe "Truisms"
        [ test "true is true" <| \_ ->
            Expect.equal True True
        , test "false is false" <| \_ ->
            Expect.equal False False
        ]
-}
describe : String -> Array Test -> Test
describe description tests =
    tests
        |> Array.map (Task.map unwrap)
        |> Task.sequence
        |> Task.map (Test.describe description)
        |> Task.map Ready


-- FUZZ TESTING



-- UTIL


getColorDepth : Maybe Terminal.Configuration -> Int
getColorDepth config =
    when config is
        Just { colorDepth } ->
            colorDepth
        Nothing ->
            1 -- lowest color depth (no colors)
